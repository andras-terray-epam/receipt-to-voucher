group 'hu.terray.andras.receipttovoucher'
version '1.0-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

description = 'Receipt To Vaucher Service'
mainClassName = 'hu.terray.receipttovoucher.ReceiptToVoucherService'

sourceCompatibility = 1.8

project.ext {
    configPath = "$rootProject.projectDir/src/dist/"
    dropwizardVersion = '1.0.6'
    guiceVersion = '4.1.0'
    dropwizardGuicePluginVersion = '1.0.0.2'
    mongoJackVersion = '2.6.1'
}


repositories {
    mavenCentral()
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'com.hubspot.dropwizard', name: 'dropwizard-guice', version: dropwizardGuicePluginVersion
    compile group: 'com.google.inject', name: 'guice', version: guiceVersion
    compile group: 'org.mongojack', name: 'mongojack', version: mongoJackVersion
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

checkstyle {
    configFile = file("${rootDir}/env/checkstyle/checkstyle.xml")
    configProperties = [samedir: "${rootDir}/env/checkstyle"]
    toolVersion = '6.10.1'
    ignoreFailures = false
}

pmd {
    ruleSetFiles = files("${rootDir}/env/pmd/pmd_ruleset.xml")
    toolVersion = '5.3.4'
    ignoreFailures = false
}

findbugs.ignoreFailures = false

[checkstyleTest, pmdTest, findbugsTest]*.enabled = false

run {
    args 'server', configPath + 'app_config.yml'
}
